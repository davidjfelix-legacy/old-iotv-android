apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        release {
            keyAlias 'key0'
            keyPassword System.getenv('key_password')
            storeFile file('releaseKeystore.jks')
            storePassword System.getenv('store_password')
        }
        debug {
            keyAlias 'key0'
            keyPassword 'debugPassword'
            storeFile file('debugKeystore.jks')
            storePassword 'debugPassword'
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "io.iotv.app"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 13
        versionName "0.0.13"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            zipAlignEnabled true
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Android support libraries
    compile 'com.android.support:support-v13:25.3.0'
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:support-v4:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Kotlin stdlib
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"


    // Retrofit HTTP client with okhttp and okio
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okio:okio:1.11.0'

    // RxJava and the mad Rx insanity
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxkotlin:2.0.0'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') { transitive = true }

    // Anko resources
    compile 'org.jetbrains.anko:anko-sdk23:0.9.1'
    compile 'org.jetbrains.anko:anko-appcompat-v7:0.9.1'
    compile 'org.jetbrains.anko:anko-design:0.9.1'
    compile 'org.jetbrains.anko:anko-recyclerview-v7:0.9.1'

    // Firebase
    compile 'com.google.firebase:firebase-database:10.2.1'
    compile 'com.google.firebase:firebase-auth:10.2.1'
    compile 'com.google.firebase:firebase-storage:10.2.1'

    // Google Sign in
    compile 'com.google.android.gms:play-services-auth:10.2.1'

    // Facebook Sign in
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'

    // AndroidTest Compile
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Test Compile
    testCompile 'junit:junit:4.12'
}

repositories {
    jcenter()
    maven {
        url 'https://maven.fabric.io/public'
    }
}


kapt {
    generateStubs = true
}

// This has to be at the end. Because gradle is stupid.
apply plugin: 'com.google.gms.google-services'
